ACTION_IF zst_generic_variants AND NOT FILE_EXISTS_IN_GAME ~ZSIPRAP0.ITM~ BEGIN

    COPY ~%MOD_FOLDER%/items/generic/rapier~ ~override~
    COPY_EXISTING ~ZSIPRAP0.ITM~ ~override~
                  ~ZSIPRAP1.ITM~ ~override~
                  ~ZSIPRAP2.ITM~ ~override~
                  ~ZSIPRAP3.ITM~ ~override~
        SAY NAME1 @80110
        SAY NAME2 @80110
        SAY UNIDENTIFIED_DESC @80111
        SAY IDENTIFIED_DESC @80111

    COPY_EXISTING ~ZSIPRAP1.ITM~ ~override~
        SAY NAME2 @80112
        SAY IDENTIFIED_DESC @80113


    COPY_EXISTING ~ZSIPRAP2.ITM~ ~override~
        SAY NAME2 @80114
        SAY IDENTIFIED_DESC @80115


    COPY_EXISTING ~ZSIPRAP3.ITM~ ~override~
        SAY NAME2 @80116
        SAY IDENTIFIED_DESC @80117


    ACTION_IF GAME_IS ~bgee eet~ BEGIN

        // +0
        COPY_EXISTING ~INN2616.STO~  ~override~ // winthrop
                      ~INN3304.STO~  ~override~ // jovial jugler
                      ~STO1112.STO~  ~override~ // general store
                      ~STO1320.STO~  ~override~ // general store
                      ~STO4803.STO~  ~override~ // nashkel store
                      ~STO4908.STO~  ~override~ // carnival shop
                      ~STOBLACK.STO~ ~override~ // black lily
                      ~STOSILEN.STO~ ~override~ // shop of silence
                      ~TAERUM.STO~   ~override~ // thunderhammer's smithy
                      ~TAV0810.STO~  ~override~ // maltz's weapon shop
                      ~ULGOTH.STO~   ~override~ // ulgoth's beard store
                      ~FRIEND.STO~   ~override~ // friendly arm inn
                      ~bdbelega.STO~ ~override~ // Belegarm's store (quartermaster SoD)
            SET amount = RANDOM(2 7)
            LPF ADD_STORE_ITEM_EX INT_VAR charge1=amount STR_VAR item_name="ZSIPRAP0" position="LAST" flags=~identified~ END
        BUT_ONLY IF_EXISTS

        // +1
        COPY_EXISTING ~INN3351.STO~  ~override~ // feldepost inn
                      ~STO0703.STO~  ~override~ // sorcerous sundries
                      ~STO1117.STO~  ~override~ // general store
                      ~STO4907.STO~  ~override~ // carnival shop
                      ~TAERUM.STO~   ~override~ // thunderhammer's smithy
                      ~bdbelega.STO~ ~override~ // Belegarm's store (quartermaster SoD)
            SET amount = RANDOM(1 2)
            LPF ADD_STORE_ITEM_EX INT_VAR charge1=amount STR_VAR item_name="ZSIPRAP1" position="LAST" flags=~identified~ END
        BUT_ONLY IF_EXISTS

        ACTION_IF zst_give_skie_bgee_rapier1 BEGIN
            // skie uses a rapier now, since it's a noble weapon
            COPY_EXISTING ~SKIE.CRE~ ~override~
                ADD_CRE_ITEM ~ZSIPRAP1~ #0 #0 #0 ~none~ ~weapon2~ EQUIP // CHECK COMPATIBILITY WITH LAVA'S MOD
        END
    END

    ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
        ACTION_IF zst_give_nalia_soa_rapier0 BEGIN
        // nalia uses a rapier now, since it's a noble weapon
            COPY_EXISTING ~NALIA8.CRE~ ~override~
                LPF IS_WEAPON_EQUIPPED INT_VAR at_slot=1 STR_VAR weapon=~SW1H07~ RET found END
                PATCH_IF found BEGIN
                    ADD_CRE_ITEM ~ZSIPRAP0~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
                END
        END

    END

END
