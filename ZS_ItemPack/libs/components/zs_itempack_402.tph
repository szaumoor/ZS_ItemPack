ACTION_IF (VARIABLE_IS_SET ~zs_itempack_402~) BEGIN
  FAIL ~Script already included: zs_itempack_402~
END ELSE BEGIN
  OUTER_SET ~zs_itempack_402~ = 1
END

COPY ~%MOD_FOLDER%/items/04_me/omniblade~ ~override~
COPY_EXISTING ~ZSIPOMN0.itm~ ~override~
    SAY NAME2 @10113
    SAY IDENTIFIED_DESC @10114
    WRITE_LONG 0x34 zst_price_base_omniblade

COPY_EXISTING ~ZSIPOMN1.itm~ ~override~
    SAY NAME2 @10115
    SAY IDENTIFIED_DESC @10116

COPY_EXISTING ~ZSIPOMN2.itm~ ~override~
    SAY NAME2 @10117
    SAY IDENTIFIED_DESC @10118

COPY_EXISTING ~ZSIPOMN3.itm~ ~override~
    SAY NAME2 @10119
    SAY IDENTIFIED_DESC @10120

COPY_EXISTING ~ZSIPOMN4.itm~ ~override~
    SAY NAME2 @10121
    SAY IDENTIFIED_DESC @10122

COPY_EXISTING ~ZSIPOMN5.itm~ ~override~
    SAY NAME2 @10123
    SAY IDENTIFIED_DESC @10124

COPY_EXISTING ~ZSIPOMN6.itm~ ~override~
    SAY NAME2 @10125
    SAY IDENTIFIED_DESC @10126

COPY_EXISTING ~SPCL915.SPL~ ~override~
    PATCH_FOR_EACH enchantment IN 0 1 2 3 4 5 6 BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR opcode=180 target=1 timing=9 parameter1=RESOLVE_STR_REF(@10127) STR_VAR resource=~ZSIPOMN%enchantment%~ END
    END

OUTER_SET thief = IDS_OF_SYMBOL(class THIEF)
OUTER_SET fighter_thief = IDS_OF_SYMBOL(class FIGHTER_THIEF)
OUTER_SET cleric_thief = IDS_OF_SYMBOL(class CLERIC_THIEF)
OUTER_SET bard = IDS_OF_SYMBOL(class BARD)

COPY_EXISTING_REGEXP ~^.+\.cre$~ ~override~
    SET class = BYTE_AT 0x273
    PATCH_IF class = thief OR class = fighter_thief OR class = cleric_thief OR class = bard BEGIN

        PATCH_FOR_EACH enchantment IN 0 1 2 3 4 5 6 BEGIN
            LPF ADD_CRE_EFFECT INT_VAR opcode=180 target=1 timing=9 parameter1=RESOLVE_STR_REF(@10127) STR_VAR resource=~ZSIPOMN%enchantment%~ END
        END

    END
BUT_ONLY

ACTION_IF GAME_IS "bgee eet" BEGIN

    COPY_EXISTING ~INN3351.STO~  ~override~ // sorcerous sundries
        LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 STR_VAR item_name="ZSIPOMN0" position="LAST" flags=~identified~ END
    BUT_ONLY IF_EXISTS

END

ACTION_IF GAME_IS "bg2ee eet" BEGIN

    COPY_EXISTING ~DUEMAG02.CRE~ ~override~
        ADD_CRE_ITEM ~ZSIPOMN0~ #0 #0 #0 ~unstealable~ ~inv~

END
