ACTION_IF (VARIABLE_IS_SET ~zs_itempack_101~) BEGIN
  FAIL ~Script already included: zs_itempack_101~
END ELSE BEGIN
  OUTER_SET ~zs_itempack_101~ = 1
END

COPY ~%MOD_FOLDER%/items/01_forbidden_game/everburn_blade~ ~override~
COPY_EXISTING ~ZSIPS2AA.itm~ ~override~
  SAY NAME2 @5001
  SAY IDENTIFIED_DESC @5002
  WRITE_LONG 0x34 zst_price_everburn_blade

COPY_EXISTING ~ZSIPS203.spl~ ~override~
  PATCH_FOR_EACH p1 IN 1 2 BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = p1 parameter1 = RESOLVE_STR_REF(@5003) END
  END

COPY_EXISTING ~ZSIPS203.spl~ ~override~
              ~ZSIPS206.spl~ ~override~
  PATCH_FOR_EACH p1 IN 1 2 3 4 BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode = 139 match_parameter1 = p1 parameter1 = RESOLVE_STR_REF(@5003) END
  END

ACTION_IF GAME_IS "bgee eet" BEGIN

  COPY_EXISTING ~SIMDEMON.CRE~ ~override~
    ADD_CRE_ITEM ~ZSIPS2AA~ #0 #0 #0 ~unstealable&undroppable~ ~weapon1~ EQUIP

  COPY_EXISTING ~SIMMEON.CRE~ ~override~
    REPLACE_CRE_ITEM ~ZSIPS2AA~ #0 #0 #0 ~unstealable~ ~inv~

END

ACTION_IF GAME_IS "bg2ee eet" BEGIN

  COPY_EXISTING ~IOTYUGH.CRE~ ~override~
    ADD_CRE_ITEM ~ZSIPS2AA~ #0 #0 #0 ~unstealable~ ~inv~

END
