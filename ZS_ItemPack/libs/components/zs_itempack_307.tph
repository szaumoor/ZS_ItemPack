ACTION_IF (VARIABLE_IS_SET ~zs_itempack_307~) BEGIN
  FAIL ~Script already included: zs_itempack_307~
END ELSE BEGIN
  OUTER_SET ~zs_itempack_307~ = 1
END

COPY ~%MOD_FOLDER%/items/03_nwn/rashemi_blade~ ~override~
COPY_EXISTING ~ZSIPRSH0.itm~ ~override~
  SAY NAME2 @8015
  SAY IDENTIFIED_DESC @8016
  WRITE_LONG 0x34 zst_price_rashemi_blade_bgee

COPY_EXISTING ~ZSIPRSH1.itm~ ~override~
  SAY NAME2 @8015
  SAY IDENTIFIED_DESC @8017
  WRITE_LONG 0x34 zst_price_rashemi_blade_bg2ee

COPY_EXISTING ~ZSIPRSH2.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF(@80150) END

COPY_EXISTING ~E32.cre~      ~override~
              ~TTMINSC.cre~  ~override~
  LPF ADD_CRE_EFFECT INT_VAR opcode = 206 timing = 9 STR_VAR resource = "ZSIPMNC2" END

ACTION_IF GAME_IS "bg2ee eet" BEGIN

  COPY_EXISTING ~MINSC8.cre~   ~override~
                ~MINSC9.cre~   ~override~
                ~MINSC10.cre~  ~override~
                ~MINSC12.cre~  ~override~
                ~MINSC14.cre~  ~override~
                ~OHB1MNSC.cre~ ~override~
    LPF ADD_CRE_EFFECT INT_VAR opcode = 206 timing = 9 STR_VAR resource = "ZSIPMNC2" END
  BUT_ONLY IF_EXISTS

  COPY_EXISTING ~UHRANG01.CRE~ ~override~
    ADD_CRE_ITEM ~ZSIPRSH1~ #0 #0 #0 ~none~ ~inv~

END ELSE BEGIN

  OUTER_SET founds = 0
  COPY_EXISTING ~MINSC.cre~   ~override~
                ~MINSC2.cre~  ~override~
                ~MINSC4.cre~  ~override~
                ~MINSC6.cre~  ~override~
    LPF ADD_CRE_EFFECT INT_VAR opcode = 206 timing = 9 STR_VAR resource = "ZSIPMNC2" END
    LPF IS_WEAPON_EQUIPPED INT_VAR at_slot=1 STR_VAR weapon=~SW2H01~ RET found END

    PATCH_IF found BEGIN
      ++founds
      ADD_CRE_ITEM ~ZSIPRSH0~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
    END

  ACTION_IF founds = 0 BEGIN
    COPY_EXISTING ~gnarl.cre~ ~override~
      ADD_CRE_ITEM ~ZSIPRSH0~ #0 #0 #0 ~none~ ~inv~
  END

  COPY_EXISTING ~MINSC7.cre~ ~override~
    LPF ADD_CRE_EFFECT INT_VAR opcode = 206 timing = 9 STR_VAR resource = "ZSIPMNC2" END
    LPF IS_WEAPON_EQUIPPED INT_VAR at_slot=1 STR_VAR weapon=~SW2H02~ RET found END

    PATCH_IF found BEGIN
      ADD_CRE_ITEM ~ZSIPRSH0~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
    END

END
