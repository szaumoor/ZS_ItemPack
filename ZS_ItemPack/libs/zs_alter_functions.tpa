DEFINE_PATCH_FUNCTION CRE_OP_ANY
INT_VAR
    match_op = "-1"
    match_p2 = "-1"
RET
    result
BEGIN
   // LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    PATCH_IF match_op = "-1" BEGIN
        PATCH_FAIL "No opcode to match was passed"
    END
    SET result = 0
    READ_LONG 0x2c4 effects_offset
    READ_LONG 0x2c8 effects_num

    READ_BYTE 0x33 eff_version
    PATCH_IF eff_version = 0 BEGIN
        SET effects_size = 0x30
    END ELSE BEGIN
        SET effects_size = 0x108
    END

    FOR ( index = 0 ; index < effects_num; ++index ) BEGIN

        PATCH_IF eff_version = 0 BEGIN
            SET opcode = SHORT_AT(effects_offset)
        END ELSE BEGIN
            SET opcode = BYTE_AT(effects_offset + 0x08)
        END

        PATCH_IF opcode = match_op BEGIN // opcode matched

            PATCH_IF eff_version = 0 BEGIN
                SET p2 = SHORT_AT(effects_offset + 0x08)
            END ELSE BEGIN
                SET p2 = BYTE_AT(effects_offset + 0x18)
            END
            SET p2 = LONG_AT (effects_offset + 0x18)

            PATCH_IF match_p2 = "-1" BEGIN // parameter2 not sought
                SET result = 1
                index = effects_num
            END ELSE PATCH_IF match_p2 >= 0 AND match_p2= p2  BEGIN // p2 sought & matched
                SET result = 1
                index = effects_num
            END
        END
        effects_offset += effects_size

    END
END

DEFINE_PATCH_FUNCTION ITM_OP_ANY
INT_VAR
    match_op = "-1"
    match_p2 = "-1"
RET
    result
BEGIN
   // LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    PATCH_IF match_op = "-1" BEGIN
        PATCH_FAIL "No opcode to match was passed"
    END
    SET result = 0
    READ_LONG  0x6a effects_offset
    READ_SHORT 0x70 effects_num
    FOR ( index = 0 ; index < effects_num; ++index ) BEGIN
        SET opcode = SHORT_AT(effects_offset)
        PATCH_IF opcode = match_op BEGIN // opcode matched
            SET p2 = LONG_AT (effects_offset + 0x08)
            PATCH_IF match_p2 = "-1" BEGIN // parameter2 not sought
                SET result = 1
                index = effects_num
            END ELSE PATCH_IF match_p2 >= 0 AND match_p2= p2 BEGIN // p2 sought & matched
                SET result = 1
                index = effects_num
            END
        END
        effects_offset += 0x30
    END
END


DEFINE_PATCH_FUNCTION PATCH_SPL_WITH_OP
INT_VAR
    match_op = "-1"
    match_p2 = "-1"
STR_VAR
    res = ~~
RET
    result
BEGIN
   // LPF CHECK_EXTENSION STR_VAR extension = "CRE" END
    PATCH_IF match_op = "-1" BEGIN
        PATCH_FAIL "No opcode to match was passed"
    END
    PATCH_IF match_p2 = "-1" BEGIN
        PATCH_FAIL "No p2 to match was passed"
    END

    SET result = 0
    SET header = 1
    GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x28
    PHP_EACH ab_array AS int => ab_off BEGIN
        SET found = 0
        GET_OFFSET_ARRAY2 fx_array ab_off SPL_V10_HEAD_EFFECTS
        PHP_EACH fx_array AS int => fx_off BEGIN
            SET opcode = SHORT_AT(fx_off)
            SET parameter2 = LONG_AT(fx_off + 0x08)
            PATCH_IF found = 0 AND opcode = match_op AND parameter2 = match_p2 BEGIN
                found = 1
                result = 1
                SET duration = LONG_AT(fx_off + 0xe)
                SET timing = BYTE_AT(fx_off + 0xc)
                SET resist_dispel = BYTE_AT(fx_off + 0xd)
                SET probability1 = BYTE_AT(fx_off + 0x12)
                SET probability2 = BYTE_AT(fx_off + 0x13)
                SET savingthrow = LONG_AT(fx_off + 0x24)
                SET savebonus = LONG_AT(fx_off + 0x28)
                SET target = BYTE_AT(fx_off + 0x2)
                SET power = BYTE_AT(fx_off + 0x3)
                LPF ADD_SPELL_EFFECT INT_VAR header target power opcode = 206 parameter1 = "-1" timing duration resist_dispel probability1 probability2 savingthrow savebonus STR_VAR resource = ~%res%~ END
            END
        END
        ++header
    END
END
